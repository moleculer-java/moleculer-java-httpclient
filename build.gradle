apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'com.bmuschko.nexus'
apply plugin: 'jacoco'

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
    }
}

// --- JAVADOC ---

javadoc {
	options.encoding = 'UTF-8'
	failOnError = false
}

// --- JACOCO ---

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    } 
}

// --- CONFIGURATIONS ---

configurations {
    runtime
    ecj
}

configurations.all {
    // resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

// --- REPOSITORIES ---

repositories {
    mavenCentral()
    jcenter()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
}

// --- DEPENDENCIES ---

dependencies {

    // ================ COMPILATION ================
	
    testCompile 'junit:junit:4.12'
	ecj 'org.eclipse.jdt.core.compiler:ecj:4.4.2'

    // =============== HTTP CLIENT =================
        
	// https://mvnrepository.com/artifact/org.asynchttpclient/async-http-client
	compile group: 'org.asynchttpclient', name: 'async-http-client', version: '2.10.4'
    
    // ================= MOLECULER =================
	
	// Moleculer web (required)
	compile group: 'com.github.berkesa', name: 'moleculer-java-web', version: '1.3.6'
	
    // ================== LOGGING ==================
	
	// http://mvnrepository.com/artifact/org.slf4j/slf4j-api
	compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.30'
       
	// http://mvnrepository.com/artifact/org.slf4j/slf4j-jdk14
	compile group: 'org.slf4j', name: 'slf4j-jdk14', version: '1.7.30'

	// https://mvnrepository.com/artifact/org.slf4j/log4j-over-slf4j
	compile group: 'org.slf4j', name: 'log4j-over-slf4j', version: '1.7.30'

	// https://mvnrepository.com/artifact/org.slf4j/jcl-over-slf4j
	compile group: 'org.slf4j', name: 'jcl-over-slf4j', version: '1.7.30'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = 'com.github.berkesa'

// + JAR version
// version = '1.0.3-SNAPSHOT'
version = '1.0.3'

modifyPom {
    project {
        artifactId 'moleculer-java-httpclient'
        name 'Promise-based HTTP client for the Moleculer Applications'
        description 'Non-blocking, Promise-based HTTP client for Moleculer Applications.'
        url 'https://moleculer-java.github.io/moleculer-java-httpclient/'
        inceptionYear '2019'

        scm {
            url 'https://moleculer-java.github.io/moleculer-java-httpclient/'
            connection 'scm:https://github.com/moleculer-java/moleculer-java-httpclient.git'
            developerConnection 'scm:git://github.com/moleculer-java/moleculer-java-httpclient.git'
        }

        licenses {
            license {
                name "The MIT License"
                url "http://www.opensource.org/licenses/MIT"
                distribution 'repo'
            }
        }
        
        dependencies {
            dependency {
                groupId 'com.github.berkesa'
                artifactId 'moleculer-java-web'
                version '1.3.6'
                scope 'runtime'
            }
            dependency {
                groupId 'org.asynchttpclient'
                artifactId 'async-http-client'
                version '2.10.4'
                scope 'runtime'
            }
        }

        developers {
            developer {
                id 'berkesa'
                name 'Andras Berkes'
                email 'andras.berkes@programmer.net'
            }
        }
    }
}

nexus {
    sign = true
    repositoryUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
	snapshotRepositoryUrl = 'https://oss.sonatype.org/content/repositories/snapshots'
}

// --- COMPILATION ---

compileJava {
	options.fork = true
	options.forkOptions.with {
		executable = 'java'
		jvmArgs = ['-classpath', project.configurations.ecj.asPath, 'org.eclipse.jdt.internal.compiler.batch.Main', '-nowarn']
	}
}

// --- JAR ---

jar {
    baseName = 'moleculer-httpclient'
    version =  '1.0.3'
}